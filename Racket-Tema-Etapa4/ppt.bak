#lang racket
(require (lib "trace.ss"))

(provide (all-defined-out))

;; Dacă ne interesează doar al n-lea TPP din arbore, este
;; convenabil să determinăm secvența de transformări care
;; conduce la acest TPP, așa cum am procedat până acum.
;;
;; În schimb, dacă ne interesează primele n TPP (sau în
;; general o secvență mai lungă de TPP) ar fi de preferat
;; crearea unui flux infinit care să le conțină pe toate
;; în ordine.
;;
;; Observăm că această ordine corespunde unei parcurgeri
;; BFS a arborelui infinit. Acesta este un BFS mai simplu
;; decât BFS-ul uzual
;; (https://en.wikipedia.org/wiki/Breadth-first_search),
;; întrucât succesorii unui TPP sunt automat triplete noi,
;; deci nu este necesar să verificăm dacă un nod a mai
;; fost sau nu vizitat.
;; 
;; Schema acestui BFS simplificat este:
;;  1. inițializăm coada de noduri care trebuie vizitate cu
;;     rădăcina arborelui (tripletul (3,4,5))
;;  2. adăugăm primul nod din coadă în rezultat
;;  3. adăugăm cei 3 succesori ai săi în coada de noduri
;;     care trebuie vizitate
;;  4. revenim la pasul 2 (întrucât construim un flux
;;     infinit, nu există condiție de oprire, și toate
;;     structurile sunt fluxuri: atât coada cât și
;;     rezultatul funcției BFS)

;; Vom refolosi matricile T1, T2, T3:
(define T1 '((-1 2 2) (-2 1 2) (-2 2 3)))
(define T2 '( (1 2 2)  (2 1 2)  (2 2 3)))
(define T3 '((1 -2 2) (2 -1 2) (2 -2 3)))

; TODO
; Aduceți aici (nu sunt necesare modificări) implementările
; funcțiilor dot-product și multiply din etapa 1 sau 2.
; Cele două funcții nu sunt re-punctate de checker, însă 
; sunt necesare generării succesorilor unui nod.
(define (dot-product X Y)
  (apply + (map * X Y)))

(define (multiply M V)
  (foldl (lambda (X Y) (append Y (list (dot-product X V)))) '() M))

; TODO
; Definiți fluxul infinit de TPP folosind algoritmul descris
; (parcurgerea BFS a arborelui infinit).
; Funcție utilă: stream-append
; Folosiți cel puțin o formă de let.

;; funcția list->stream, care creează un flux din elementele unei liste
(define (list->stream L)
  (if (null? L)
      empty-stream
      (stream-cons (car L) (list->stream (cdr L)))))

; ma comport de parca as avea un arbore;
; fiecare nod e un ppt, iar fircare muchie e o transformare
; fac o parcurgere in latime
;         aplic T1-> (15 8 17)
;        |
; (3 4 5)- aplic T2-> (21 20 22)
;        |
;        - aplic T3-> (15 12 13)
;;; functie preluata partial din curs
(define (lazy-breadth-search init-tuple expand-function)
  (let make-search ([states (stream-cons init-tuple empty-stream)])
    (stream-cons (stream-first states)
                 (make-search (stream-append (stream-rest states)
                                             (expand-function (stream-first states)))))))

;; tripletul de start
(define start-ppt '(3 4 5))
;; lista transformarilor
(define transformations (list T1 T2 T3))

; aplic parcurgerea in latime a arborelui
(define ppt-stream-in-tree-order
  (lazy-breadth-search start-ppt (lambda (state) (stream-map (lambda (x) (multiply x state)) (list->stream transformations)))))

;; Un alt mod de a genera TPP se folosește de perechi (g, h)
;; care indeplinesc condițiile:
;;    g, h impare
;;    g < h
;;    g, h prime între ele
;;
;; Nu întâmplător am ales aceste notații, teoria este aceeași
;; cu cea din spatele cvartetelor (g, e, f, h), pe care le
;; putem exprima și ca (g, (h-g)/2, (h+g)/2, h).
;;
;; Pentru a obține un TPP dintr-o pereche (g, h) se aplică
;; aceleași formule (dar le vom exprima în funcție de g și h):
;;    a = gh
;;    b = 2ef = (h - g)(h + g) / 2
;;      = (h^2 - g^2) / 2
;;    c = e^2 + f^2 = (h - g)^2 / 4 + (h + g)^2 / 4
;;      = (h^2 + g^2) / 2
;;
;; Acest mod de generare ne furnizează TPP în altă ordine
;; decât cea dată de parcurgerea în lățime a arborelui TPP.
;;
;; Noua ordine se obține parcurgând pe coloane diagrama:
;;                        h      
;;         3     5     7     9     11   .  .  .
;;    1  (1,3) (1,5) (1,7) (1,9) (1,11) .  .  .
;;    3        (3,5) (3,7)   -   (3,11) .  .  .
;;    5              (5,7) (5,9) (5,11) .  .  .
;; g  7                    (7,9) (7,11) .  .  .
;;    9                          (9,11) .  .  .
;;    .                                 .  .  .
;;    .                                    .  .
;;    .                                       .
;; (lipsește perechea (3,9), 3 și 9 nefiind prime între ele)
;;
;; Folosind această indexare, primele 6 TPP sunt:
;;    (3,4,5)                           - din perechea (1,3)
;;    (5,12,13), (15,8,17)              - din (1,5), (3,5)
;;    (7,24,25), (21,20,29), (35,12,37) - din (1,7), (3,7), (5,7)
;;
;; Ne propunem să definim fluxul infinit de TPP în ordinea de
;; mai sus. Acesta se bazează pe fluxul corespunzător de 
;; perechi (g, h), pe care îl generăm astfel:
;;  - pornim cu 2 fluxuri infinite:
;;    * G = 1, 3, 5, 7 ...
;;    * H = 3, 5, 7, 9 ... (întrucât g < h)
;;  - fluxul ordonat pe coloane va conține:
;;    * perechea compusă din cele mai mici numere din G și H
;;      (ex: (1,3))
;;    * apoi interclasarea (conform ordinii "pe coloane") între:
;;      - perechile compuse dintre minimul din G și restul din H
;;        (ex: (1,5), (1,7), (1,9) ...)
;;      - fluxul ordonat generat de restul lui G și restul lui H
;;        (ex: (3,5), (3,7), (5,7) ...)
;; Aceasta este abordarea generală, în urma căreia generăm toate
;; perechile, inclusiv pe cele de numere care nu sunt prime  
;; între ele. Perechile neconforme trebuie înlăturate ulterior
;; (utilizând funcția de bibliotecă gcd).


; TODO
; Definiți o funcție care primește 2 fluxuri numerice infinite
; G și H, și generează fluxul de perechi de câte un element 
; din G și unul din H ordonate conform metodei de mai sus.
; Condițiile ca g și h să fie impare, prime între ele, respectiv
; menținerea restricției g < h (cât timp urmați algoritmul) nu
; trebuie impuse în implementarea funcției pairs.
; Ele vor fi asigurate de definirea fluxurilor de mai jos prin:
;  - apelarea lui pairs exclusiv pe fluxurile
;    G = 1, 3, 5, 7 ... și H = 3, 5, 7, 9 ...
;  - eliminarea perechilor de numere neprime între ele (care 
;    există în rezultatul funcției pairs, dar nu vor mai exista
;    în fluxul gh-pairs-stream)

; am multimile de numere G si H
; tin 2 indexi pentru G si pentru H: - cel pentru g incepe de la 0, cel pentru h de la 1
; daca indexii sunt egali, "elimin" primul element din h (adica trec la urmatoarele elemente din h)
; si o iau de la inceeput cu elementele din G
; altfel, cresc index-ul lui g si index-ul lui h ramane neschimbat; inaintez in g si din h iau primul element
(define (pairs G H)
  (let make-pairs ([g G] [h H] [index-g 0] [index-h 1])
    (if (equal? index-g index-h)
        (make-pairs G (stream-rest h) 0 (add1 index-g))
        (stream-cons (cons (stream-first g) (stream-first h)) (make-pairs (stream-rest g) h (add1 index-g) index-h)))))

; TODO
; Definiți fluxul de perechi (g, h) pe care se bazează noua
; indexare a TPP.
; Nu folosiți recursivitate explicită (decât pentru a genera
; fluxurile de pornire - G și H).

; stream-ul numerelor naturale
(define naturals
  (let loop ((n 0))
    (stream-cons n (loop (add1 n)))))

; din stream-ul numerelor naturale, tin doar cele impare
(define odd-naturals
  (stream-filter odd? naturals))

; pornind de la multimea nr impare de la 1 si multimea nr impare de la 3,
; tin doar perechile care sunt prime intre ele (folosesc o functie lambda pt asta)
; folosesc stream-filter
(define gh-pairs-stream
  (stream-filter (lambda (x) (if (equal? 1 (gcd (car x) (cdr x))) true false)) (pairs odd-naturals (stream-rest odd-naturals))))                       

; TODO
; Definiți fluxul de TPP corespunzător fluxului anterior de
; perechi (g, h).

;;    a = gh
;;    b = 2ef = (h - g)(h + g) / 2
;;      = (h^2 - g^2) / 2
;;    c = e^2 + f^2 = (h - g)^2 / 4 + (h + g)^2 / 4
;;      = (h^2 + g^2) / 2

; functii care calculeaza a b si c pt o pereche data x (care contine g si h)
(define (calculate-a x)
  (* (car x) (cdr x)))

(define (calculate-b x)
  (/ (- (* (cdr x) (cdr x)) (* (car x) (car x))) 2))

(define (calculate-c x)
  (/ (+ (* (cdr x) (cdr x)) (* (car x) (car x))) 2))

; pt fiecare pereche din stream-ul gh, fac un map cu functia lambda care returneaza lista formata din a b c
(define ppt-stream-in-pair-order
  (stream-map (lambda (x) (list (calculate-a x) (calculate-b x) (calculate-c x))) gh-pairs-stream))